\documentclass{eplDoc}



\newcommand{\docType}	{Assignment 2}
\newcommand{\docDate}	{09/03/2012}
\newcommand{\docAuthor}	{gr16 : Mulders Corentin, Pelsser Francois}
\newcommand{\courseCode}{LINGI2365}
\newcommand{\courseName}{Constraint programming}
\usepackage{syntax}

\lstset{breaklines=true, breakatwhitespace=false}

\begin{document}
\maketitle
\newpage

\section{The social golfer problem}

\subsection{Different possible models for the problem}
\subsubsection{First model}
Our first model uses a slot (each slot represents a group) number in wich a players plays for each week as variable. The constraints are then easy to express : 
\begin{lstlisting}
Solver<CP> cp();

int n = 1;                   //Number of weeks
range G = 1..32;               //Golfers ids
range W = 1..n;                //Weeks ids
range S = 1..(G.getUp()/4);    //Slots ids


var<CP>{int} GPlanning[G,W](cp, S);   

solve<cp>{
     // each group contains 4 golfers
    forall(w in W,s in S)
            cp.post((sum(g in G) (GPlanning[g,w] == s)) == 4);

    //each pair of golfers plays at most one time together
    forall(g1 in G, g2 in G: g1 != g2)
        cp.post(sum(w in W)(GPlanning[g1,w] == GPlanning[g2,w]) <= 1);

} using{

    // Use a generic search procedure
    label(cp);

}
\end{lstlisting}

The advantage of this model is that the decision variable is well chosen for the problem since there will be only one group chosen each week for each golfer. \\ 
This variable also allows to write simple constraints. \\ 

\subsubsection{Second model}
For our second model we considered using a different variable. The variable used is the golfer number that is playing in a given place in a group. \\ 
Then we have to write constraints so that each player can be placed in only one place each week and that a golfer has different opponents each week. 

\begin{lstlisting}
Solver<CP> cp();

int n = 6;                   //Number of weeks
range G = 1..32;               //Golfers
range W = 1..n;                //Weeks
range S = 1..(G.getUp()/4);    //groups
range P = 1..4;								 //places

var<CP>{int} GPlanning[W,S,P](cp, G);   

solve<cp>{
   //not the same golfer in more than one place each week
    forall(w in W)
        cp.post(alldifferent(all(s in S, p in P)GPlanning[w,s,p]));
    
   //a golfer does not play more than once against another golfer
   forall(g1 in G, g2 in G: g1 != g2)
        cp.post((sum(w in W, s in S, p1 in P, p2 in P) (GPlanning[w,s,p1] == g1 && GPlanning[w,s,p2]==g2)) <= 1); 


} using{

    // Use a generic search procedure
    label(cp);
}
\end{lstlisting}

The main disadvantage with this model is that the variable used is not practical to express our constraints, especially the second one wich is very heavy to compute. \\ 
Moreove we add a "place" element that wasn't in the first model while it is something that is not needed for the problem since the position of a player in a group is irrelevent. \\ 
The decisionv ariable size is also bigger than the one used for our model 1. 

\subsubsection{Third model}

Our third idea was to use a variable representing the week in wich any couple of golfers plays together. To make it work we have to write a little bit more constraints than for the two previous models :  

\begin{lstlisting}
Solver<CP> cp();


int n = 2;                   //Number of weeks
range G = 1..32;               //Golfers
range W = 1..n;
range WE = 0..n;                //Weeks
range S = 1..(G.getUp()/4);    //Slots


var<CP>{int} GAgainst[G,G](cp, WE);   

solve<cp>{
    //if p1 plays against p2 at some time then p2 plays against p1 at the same time
    forall(g1 in G, g2 in G)
        cp.post(GAgainst[g1,g2] == GAgainst[g2,g1]);

    //a player doesn't play against himself
    forall(g in G)
        cp.post(GAgainst[g,g] == 0);
    
    //players from a group play with every member of the group
    forall(w in W, g1 in G, g2 in G, g3 in G : g3 != g2 && g2 != g1 && g1 != g3)
        cp.post(((GAgainst[g1,g2] != w) || (GAgainst[g1,g3] != w)) || (GAgainst[g2,g3] == w));
    
    //each player has 3 opponents each week 
    forall(g in G)
        cp.post(cardinality(all(w in W) 3 , all(g2 in G) GAgainst[g,g2]));

    //each player plays every week
    forall(g in G)
        cp.post(sum(g2 in G)(GAgainst[g,g2]) == (sum(w in W)(3*w)));
} using{

    // Use a generic search procedure
    label(cp);
}
\end{lstlisting}

This model presents a few disadvantages. \\ 
The decision variable size is as big as in our second model. \\ 
We also need to apply a lot of constraints because of this choice of variable. 

\subsection{Symmetries in the problem}

There are some symetries in this problem : 
\begin{itemize}
	\item Symetry on the weeks : every pair of weeks in the solution can be swapped. 
	\item Symetry on the groups : every group composition can be exchanged with another group from the same week. 
	\item Symetry on the golfers : any golfer's complete schedule can be exchanged with the complete schedule of another golfer. However if the two were supposed to play against each other at some time their IDs have to be exchanged in the respective schedules too.
\end{itemize}

We tried to use these symetries to improve our first model by reducing the search space. To do this we added two variables. \\ 
One keeps track of the lowest bound for the golfers IDs in each slot for each week. This allows us to add constraints that make sure that the slots are numbered in an order determined by the lowest golfer ID from the slot. This removes the possibility to swap two slots. \\ 
The other does the same kind of thing for the weeks. \\ 
We also changed the search method to make sure to not assign values that are not in the domain of the variables anymore. \\ \\ 

However this only made the model worst because it already finds the solution without searching much so by adding constraints and variable we complicated it to no avail. In the end we chose to stick to the original model that worked better. \\ 





\subsection{Maximum number of weeks identified in a reasonable time limit}
The maximum number of weeks we could identify in a reasonable time limit is 9 weeks using our first model. It took around 100ms to find a solution for 9 weeks but when searching for a solution for 10 weeks we kill the program after 2min. \\ 
To compare with our other models, the second model found a solution for 5 weeks in around 20s. And the third found a solution for 3 weeks in around 14s and was killed after 2min when trying to find one for 4 weeks. \\ 

\subsection{Time needed, number of failures and number of choices for each number of weeks}
These are results obtained with our first model since it's the most efficient one. 

\begin{center}
		\begin{tabular}{|l|c|c|c|}
			\hline
			Number of weeks & Time needed [ms] & Number of failures & Number of choices \\ 
			\hline
			1 & 15 & 0 & 28 \\ 
			2 & 47 & 0 & 56 \\ 
			3 & 62 & 0 & 79 \\ 
			4 & 47 & 0 & 99 \\ 
			5 & 109 & 0 & 118 \\ 
			6 & 78 & 8 & 147 \\ 
			7 & 63 & 8 & 160 \\ 
			8 & 109 & 34 & 195 \\ 
			9 & 109 & 34 & 202 \\ 
			\hline
		\end{tabular}
\end{center}



\section{The time tabling problem} 

\subsection{Model for the problem}
The first idea was to uses two variables.  The first is an array linking each event to a room and the second is an array linking each event to a time slot.  For speed sakes, we then added two other variables, first a Table linking each room at each time slot with an identifier, the second is an array linking each event to identifier. 
\subsubsection{Description}
In addition to this model we also precalculated some values.  We created for every event a set of room that can hold it (has enough place and all the features) and a set containing all the other event that couldn't take place during the same time slot (because a student go to the 2 events).  This allow to easier and with less computation express the constraints, this also allow to work with fewer start domains for the rooms.
\\\\
We also added two redundant constraints to reduce the domains of the rooms and the time slots for the events.
\newpage
Here is the what we had :
\begin{lstlisting}
/*
    INIT and read file
*/

range R     = 1..nbRooms;
range E     = 1..nbEvents;
range F     = 1..nbFeatures;
range S     = 1..nbStudents;
range T     = 1..nbSlots;

int roomsSizes[R];
int sAttends[S,E];  //1 if s attends to e
int rFeatures[R,F]; //1 if the room has the feature
int eFeatures[E,F]; //1 if the event need the feature


/*
    read file
*/


//size min in a room for an event
int eRoomSizeMin[E];
forall(e in E)
    eRoomSizeMin[e] = (sum(s in S)(sAttends[s,e] == 1));


//for every events what room can be used
set{int} roomsForEvent[E];
forall(e in E)
{
    forall(r in R : eRoomSizeMin[e] <= roomsSizes[r])
    {
        boolean roomOk = true;
        forall(f in F)
            if((eFeatures[e,f] == 1) && (rFeatures[r,f] != 1))
                roomOk = false;
        if(roomOk)
            roomsForEvent[e].insert(r);
    }
}

//events that cannot have the same time slot
set{int} eventNotWhen[E];
forall(e1 in E,e2 in E: e2> e1)
{
    int i = 1;
    boolean endLoop = false;
    while(i<=nbStudents && !endLoop)
    {
        if((sAttends[i,e1] == 1) && (sAttends[i,e2] == 1))
        {
            eventNotWhen[e1].insert(e2);
            endLoop = true;
        }
        i++;
    }
}

//link room slot with an id
tuple triple{int r; int t; int i;}

set{triple} Triples();
forall(r in R, t in T)
   Triples.insert(triple(r, t, ((r-1)*(T.getSize()) + t) ));


var<CP>{int} slotForEvent[E](cp, T);
var<CP>{int} roomForEvent[E](cp, R);
var<CP>{int} schedule[E](cp, 1..((nbRooms*nbSlots)+100));
Table<CP> t(all(e in Triples) e.r, all(e in Triples) e.t, all(e in Triples) e.i);


//remove all room that cannot hold an event from the domain
forall(e in E)
{
    inSet(roomForEvent[e],roomsForEvent[e]);
}


solve<cp>{
    //verify that each student has not multiple events at the same time
    forall(e1 in E, e2 in eventNotWhen[e1])
        cp.post(slotForEvent[e1] != slotForEvent[e2]);

    //redundant constraint to speed up the process
    forall(e in E)
    {
        cp.post((sum(t in T)(slotForEvent[e] == t)) <= nbRooms);
        cp.post((sum(r in R)(roomForEvent[e] == r)) <= nbSlots);
    }

    //avoid having 2 events on the same time slot in the same room
    cp.post(alldifferent(all(e in E)schedule[e]),onDomains);

    //link id to room and slots
    forall(e in E)
        cp.post(table(roomForEvent[e],slotForEvent[e], schedule[e], t));


} using{
    forall (e in E: !slotForEvent[e].bound()) by (slotForEvent[e].getSize())
        tryall<cp>(t in T: slotForEvent[e].memberOf(t))
        {
            label(slotForEvent[e], t);
        }
    
    forall (e in E: !roomForEvent[e].bound()) by (roomForEvent[e].getSize())
        tryall<cp>(r in R: roomForEvent[e].memberOf(r))
        {
            label(roomForEvent[e], r);
        }
    
}

/*
    print solution
*/
\end{lstlisting}

\subsubsection{Advantages and disadvantages}
\subsubsection{Alternative models}
We tought to a model when we had an array with two 

\subsection{Symmetries}

A possible symetry in this problem would be that every time slots pair can be exchanged. However we don't use this in our solution because it seemed unneccessary. 

\subsection{Results on test files}

As we can see in the next section, our program found a solution in around 4s for most of the provided test files. The exception are the instances 04, 09 and 19 for wich our solver seems to have more difficulties. \\ 


\subsection{Time needed, number of failures and number of choices for each instance}
\begin{center}
		\begin{tabular}{|l|c|c|c|}
			\hline
			Instance & Time needed [ms] & Number of failures & Number of choices \\ 
			\hline
			01 & 4149 & 0 & 415 \\ 
			02 & 4041 & 0 & 415 \\ 
			03 & 4258 & 0 & 464 \\ 
			04 & ? & ? & ? \\ 
			05 & 4119 & 0 & 383 \\ 
			06 & 4493 & 0 & 445 \\ 
			07 & 4742 & 0 & 374 \\ 
			08 & 5070 & 0 & 440 \\ 
			09 & ? & ? & ? \\ 
			10 & 4805 & 0 & 509 \\ 
			11 & 4602 & 0 & 414 \\ 
			12 & 4040 & 0 & 410 \\ 
			13 & 4977 & 0 & 436 \\ 
			14 & 5257 & 0 & 384 \\ 
			15 & 4243 & 0 & 360 \\ 
			16 & 5726 & 0 & 517 \\ 
			17 & 3868 & 0 & 351 \\ 
			18 & 4166 & 0 & 409 \\ 
			19 & ? & ? & ? \\ 
			20 & 4930 & 0 & 456 \\
			\hline
		\end{tabular}
\end{center}


\end{document}
